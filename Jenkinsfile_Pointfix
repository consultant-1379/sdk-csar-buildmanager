#!/usr/bin/env groovy

/* IMPORTANT:
 *
 * In order to make this pipeline work, the following configuration on Jenkins is required:
 * - slave with a specific label (see pipeline.agent.label below)
 * - credentials plugin should be installed and have the secrets with the following names:
 *   + lciadm100credentials (token to access Artifactory)
 */

def defaultBobImage = 'armdocker.rnd.ericsson.se/sandbox/adp-staging/adp-cicd/bob.2.0:1.7.0-55'
def bob = new BobCommand()
        .bobImage(defaultBobImage)
        .envVars([
                HOME:'${HOME}',
                BM_PACKAGE_NAME:'${BM_PACKAGE_NAME}',
                PWD:'${PWD}',
                ISO_VERSION: '${ISO_VERSION}',
                ERIC_ENM_FMSDK_IMAGE_TAG: '${ERIC_ENM_FMSDK_IMAGE_TAG}',
                ERIC_ENM_FMSDK_TEMPLATES: '${ERIC_ENM_FMSDK_TEMPLATES}',
		ERIC_ENM_PMSDK_IMAGE_TAG: '${ERIC_ENM_PMSDK_IMAGE_TAG}',
                ERIC_ENM_PMSDK_TEMPLATES: '${ERIC_ENM_PMSDK_TEMPLATES}',
		ENM_ISO_REPO_VERSION: '${ENM_ISO_REPO_VERSION}',
		CHART_REPO: '${CHART_REPO}'
        ])
        .needDockerSocket(true)
        .toString()
def GIT_COMMITTER_NAME = 'lciadm100'
def GIT_COMMITTER_EMAIL = 'lciadm100@ericsson.com'
def failedStage = ''
pipeline {
    agent {
        label 'Cloud-Native'
    }
    environment{
        repositoryUrl = "https://arm2s11-eiffel004.eiffel.gic.ericsson.se:8443/nexus/content/repositories/cloud-native-enm-sdk/buildmanager"
        BM_PACKAGE_NAME = "cloud-native-enm-sdk"
        OPENIDM = "eric-enm-openidm-change-password:latest"
        PASSKEY = "eric-enm-securestorage-regen-passkey:latest"
        OPENIDM_IMAGE_PATH = "armdocker.rnd.ericsson.se/proj-enm"
        PASSKEY_IMAGE_PATH = "armdocker.rnd.ericsson.se/proj-enm"
        PACKAGE_TYPE="buildmanager-csar"
        CENMBUILD_ARM_TOKEN = credentials('cenmbuild_ARM_token')
    }
    parameters {
        string(name: 'ISO_VERSION', defaultValue: '0.0.0', description: 'The ENM ISO version (e.g. 1.65.77)')
        string(name: 'BRANCH', description: 'Branch to build')
    }
    stages {
        
        stage('Clean'){
            steps{
                deleteDir()
	        }
	    }
        stage('Inject Credential Files') {
            steps {
                sh 'printenv'
                withCredentials([file(credentialsId: 'lciadm100-docker-auth', variable: 'dockerConfig')]) {
                    sh "install -m 600 ${dockerConfig} ${HOME}/.docker/config.json"
                }
            }
        }
        stage('Checkout Cloud-Native SG Git Repository') {
            steps {
                git branch: env.BRANCH,
                        credentialsId: 'lciadm100_private_key',
                        url: 'ssh://gerrit.ericsson.se:29418/'+env.REPO
            }
        }
        stage('Generate new version') {
            steps {
                sh "${bob} generate-new-version"
                script {
                    env.VERSION = sh(script: "cat .bob/var.version", returnStdout:true).trim()
                    echo "Generated VERSION is: ${VERSION}"
                    env.RSTATE = sh(script: "cat .bob/var.rstate", returnStdout:true).trim()
                    echo "Generated RSTATE is: ${RSTATE}"
                }
            }
            post {
                failure {
                    script {
                        failedStage = env.STAGE_NAME
                    }
                }
            }
        }

        stage('Swap monitoring image version'){
            steps{
                echo sh(script: 'env', returnStdout:true)
                step ([$class: 'CopyArtifact', projectName: 'sync-build-trigger-pointfix', filter: "*"])
                script {
                    def props = readProperties file: 'enm_artifact.properties'
                    env.ERIC_ENM_MONITORING_EAP7_IMAGE_TAG = props.ERIC_ENM_MONITORING_EAP7_IMAGE_TAG
                }
                sh "sed -i 's|armdocker.rnd.ericsson.se/proj-enm/eric-enm-monitoring-eap7:.*|armdocker.rnd.ericsson.se/proj-enm/eric-enm-monitoring-eap7:${env.ERIC_ENM_MONITORING_EAP7_IMAGE_TAG}|' ${WORKSPACE}/src/sdk_builld_images.txt"
                sh '''
                    if git status | grep '${WORKSPACE}/src/sdk_builld_images.txt' > /dev/null; then
                        git commit -m "NO JIRA - Updating sdk_build_images.txt with  monitoring version"
                        git push origin HEAD:master
                    else
                        echo `date` > timestamp
                        git add timestamp
                        git commit -m "NO JIRA - Time Stamp "
                        git push origin HEAD:${BRANCH}
                    fi
                '''
            }
        }

        stage('Generate Cloud Native ENM SDK BuildManager Archive') {
            steps {
                script {
                    sh "${bob} generate-bm-archive"
                }
            }
            post {
                failure {
                    script {
                        failedStage = env.STAGE_NAME
                    }
                }
            }
        }
        stage('Publish BuildManager Package to Nexus') {
            steps {
              script {
               env.filesize = sh(script: "du -h build/${BM_PACKAGE_NAME}-${VERSION}.tar.gz | cut -f1", returnStdout: true).trim()
               sh "bash upload_to_nexus.sh ${VERSION} build/${BM_PACKAGE_NAME}-${VERSION}.tar.gz ${repositoryUrl} ${BM_PACKAGE_NAME} ${PACKAGE_TYPE}"
	       env.CHART_REPO = "${ENM_ISO_REPO_VERSION}"
              }
            }
        }
        stage('Generate ADP Parameters') {
            steps {
                sh "${bob} generate-output-parameters"
                archiveArtifacts 'artifact.properties'
            }
        }
        stage('Tag sdk-csar-buildmanager Repository') {
            steps {
                wrap([$class: 'BuildUser']) {
                    script {
                        def bobWithCommitterInfo = new BobCommand()
                                .bobImage(defaultBobImage)
                                .needDockerSocket(true)
                                .envVars([
                                        'AUTHOR_NAME'        : "\${BUILD_USER:-${GIT_COMMITTER_NAME}}",
                                        'AUTHOR_EMAIL'       : "\${BUILD_USER_EMAIL:-${GIT_COMMITTER_EMAIL}}",
                                        'GIT_COMMITTER_NAME' : "${GIT_COMMITTER_NAME}",
                                        'GIT_COMMITTER_EMAIL': "${GIT_COMMITTER_EMAIL}"
                                ])
                                .toString()
                        sh "${bobWithCommitterInfo} create-git-tag"
                        sh """
                            tag_id=\$(cat .bob/var.version)
                            git push origin \${tag_id}
                        """
                    }
                }
            }
            post {
                failure {
                    script {
                        failedStage = env.STAGE_NAME
                    }
                }
                always {
                    script {
                        sh "${bob} remove-git-tag"
                    }
                }
            }
        }
        stage('Bump Version') {
            steps {
                script {
                    sh 'hostname'
                    Version = readFile "VERSION_PREFIX"
                    sh 'chmod -R 777 VERSION_PREFIX'
                    sh 'docker run --rm -v $PWD/VERSION_PREFIX:/app/VERSION -w /app armdocker.rnd.ericsson.se/proj-enm/bump patch'
                    newVersion = readFile "VERSION_PREFIX"
                    env.IMAGE_VERSION = newVersion
                    currentBuild.displayName = "${BUILD_NUMBER} - Version - " + Version
                    sh '''
                        git add VERSION_PREFIX
                        git commit -m "Version $IMAGE_VERSION"
                        git push origin HEAD:${BRANCH}
                    '''
                }
            }
        }
    }
}

import groovy.transform.builder.Builder
import groovy.transform.builder.SimpleStrategy

@Builder(builderStrategy = SimpleStrategy, prefix = '')
class BobCommand {
    def bobImage = 'bob.2.0:latest'
    def envVars = [:]
    def needDockerSocket = false

    String toString() {
        def env = envVars
                .collect({ entry -> "-e ${entry.key}=\"${entry.value}\"" })
                .join(' ')

        def cmd = """\
            |docker run
            |--init
            |--rm
            |--workdir \${PWD}
            |--user \$(id -u):\$(id -g)
            |-v \${PWD}:\${PWD}
            |-v /etc/group:/etc/group:ro
            |-v /etc/passwd:/etc/passwd:ro
            |-v \${HOME}/.m2:\${HOME}/.m2
            |-v \${HOME}/.docker:\${HOME}/.docker
            |${needDockerSocket ? '-v /var/run/docker.sock:/var/run/docker.sock' : ''}
            |${env}
            |\$(for group in \$(id -G); do printf ' --group-add %s' "\$group"; done)
            |--group-add \$(stat -c '%g' /var/run/docker.sock)
            |${bobImage}
            |"""
        return cmd
                .stripMargin()           // remove indentation
                .replace('\n', ' ')      // join lines
                .replaceAll(/[ ]+/, ' ') // replace multiple spaces by one
    }
}
